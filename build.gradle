plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.0'
}

repositories {
    jcenter()

    maven {
        url 'https://dl.bintray.com/espere-me/production/'
    }
}

dependencies {
    compileOnly group: 'me.espere.feelings', name: 'feelings-spec', version: 'v0.0.3'

    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.4'

    implementation(group: 'edu.stanford.nlp', name: 'stanford-corenlp', version: '3.8.0') {
        exclude module: 'javax.servlet-api'
    }

    testImplementation group: 'me.espere.feelings', name: 'feelings-spec', version: 'v0.0.3'
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.16.0'
}

publishing {
    publications {
        feelingsImpl(MavenPublication) {
            from components.java
            groupId 'me.espere.feelings'
            artifactId 'feelings-impl'
        }
    }
}

bintray {
    dryRun = !project.hasProperty('deploy')

    user System.getenv('BINTRAY_USER')
    key System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'production'
        name = 'feelings-impl'
        userOrg = 'espere-me'

        version {
            name = project.property('version')
        }
    }

    publications = ['feelingsImpl']

    publish = true
}

task checkVersion {
    description 'This task fails the build if the version property is not set'

    doLast {
        if (project.property('version') == 'unspecified') {
            throw new GradleException('You must specify a version property')
        }
    }
}

tasks.bintrayUpload.dependsOn checkVersion
tasks.bintrayUpload.dependsOn clean
tasks.bintrayUpload.dependsOn build

clean.mustRunAfter checkVersion
build.mustRunAfter clean
